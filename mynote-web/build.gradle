group 'com.xuyazhou.mynote'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

configurations {
    mybatisGenerator
}

// 读取config文件夹中对应的配置文件
ext {
    def prop = new Properties()
    file("src/main/resources/config/db.properties")
            .withInputStream {
        prop.load(it)
    }
    file("src/main/resources/config/mybatis/gradle.properties")
            .withInputStream {
        prop.load(it)
    }
    prop.each {
        project.extensions.add("$it.key", it.value)
    }
}

def springVersion = "4.3.7.RELEASE"
def aspectjVersion = "1.8.5"

dependencies {

    compile "org.springframework:spring-aop:${springVersion}"
    compile "org.springframework:spring-context:${springVersion}"
    compile "org.springframework:spring-context-support:${springVersion}"
    compile "org.springframework:spring-beans:${springVersion}"
    compile "org.springframework:spring-web:${springVersion}"
    compile "org.springframework:spring-webmvc:${springVersion}"
    compile "org.springframework:spring-tx:${springVersion}"
    compile "org.springframework:spring-jdbc:${springVersion}"
    compile "org.springframework:spring-aspects:${springVersion}"
    compile "org.springframework:spring-oxm:${springVersion}"
    compile "org.springframework:spring-orm:${springVersion}"
    compile 'org.springframework.data:spring-data-redis:1.8.1.RELEASE'


    compile "org.aspectj:aspectjrt:${aspectjVersion}"
    compile "org.aspectj:aspectjweaver:${aspectjVersion}"
    compile "org.aspectj:aspectjtools:${aspectjVersion}"


    compile "javax.servlet.jsp:javax.servlet.jsp-api:2.3.2-b02"
    compile "javax.servlet:javax.servlet-api:4.0.0-b05"

    compile 'mysql:mysql-connector-java:6.0.6'
    compile 'org.mybatis.generator:mybatis-generator-core:1.3.5'
    compile 'org.mybatis:mybatis-spring:1.3.1'
    compile 'org.mybatis:mybatis:3.4.2'

    compile 'org.apache.commons:commons-dbcp2:2.1.1'
    compile 'commons-codec:commons-codec:1.10'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'org.apache.commons:commons-lang3:3.5'

    compile 'com.qiniu:qiniu-java-sdk:7.2.+'


    compile 'redis.clients:jedis:2.9.0'

//    compile 'org.ehcache.modules:ehcache-core:3.1.3'
//    compile 'org.mybatis.caches:mybatis-ehcache:1.0.3'


    compile 'org.hibernate:hibernate-validator:5.4.1.Final'
    compile 'org.jboss.logging:jboss-logging:3.3.1.Final'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.8.5'
    compile 'com.fasterxml.jackson.core:jackson-core:2.8.5'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.5'


    compile 'commons-fileupload:commons-fileupload:1.3.2'


    compile 'log4j:log4j:1.2.17'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.slf4j:slf4j-log4j12:1.7.21'
    compile 'commons-collections:commons-collections:3.2.2'

    compile 'com.github.pagehelper:pagehelper:5.0.2'
    compile 'com.github.jsqlparser:jsqlparser:0.9.5'

    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    compile 'jstl:jstl:1.2'

    mybatisGenerator 'org.mybatis.generator:mybatis-generator-core:1.3.5'
    mybatisGenerator 'mysql:mysql-connector-java:6.0.6'

    testCompile 'junit:junit:4.12'
    testCompile "org.springframework:spring-test:${springVersion}"
}

// 利用ant创建一个自动生成MyBatis目录文件的task
task mybatisGenerate << {
    ant.properties['targetProject'] = projectDir.path
    ant.properties['jarDirection'] = project['jdbc.jarDirection']
    ant.properties['driverClass'] = project['jdbc.driverClassName']
    ant.properties['connectionURL'] = project['jdbc.url']
    ant.properties['userId'] = project['jdbc.user']
    ant.properties['password'] = project['jdbc.pass']
    ant.properties['src_main_java'] = sourceSets.main.java.srcDirs[0].path
    ant.properties['src_main_resources'] = sourceSets.main.resources.srcDirs[0].path
    ant.properties['modelPackage'] = project['modelPackage']
    ant.properties['mapperPackage'] = project['mapperPackage']
    ant.properties['sqlMapperPackage'] = project['sqlMapperPackage']

    ant.taskdef(
            name: 'mbgenerator',
            classname: 'org.mybatis.generator.ant.GeneratorAntTask',
            classpath: configurations.mybatisGenerator.asPath
    )

    ant.mbgenerator(overwrite: true,
            configfile: 'src/main/resources/config/mybatis/generatorConfig.xml',
            verbose: true) {
        propertyset {
            propertyref(name: 'targetProject')
            propertyref(name: 'jarDirection')
            propertyref(name: 'userId')
            propertyref(name: 'driverClass')
            propertyref(name: 'connectionURL')
            propertyref(name: 'password')
            propertyref(name: 'src_main_java')
            propertyref(name: 'src_main_resources')
            propertyref(name: 'modelPackage')
            propertyref(name: 'mapperPackage')
            propertyref(name: 'sqlMapperPackage')
        }
    }
}

